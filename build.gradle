buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.4.RELEASE")
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0')
    }
}

plugins {
    id "org.sonarqube" version "2.7"
}

group = 'whatstheweatherlike'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

bootJar {
    baseName = 'weather-service'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
}

// can't upgrade to 11 because on mockito requiring internal methods
// see https://dzone.com/articles/jdk-11-and-proxies-in-a-world-past-sunmiscunsafe
sourceCompatibility = 10
targetCompatibility = 10

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'io.micrometer:micrometer-registry-prometheus'
    testCompile 'org.awaitility:awaitility:3.1.6'
}

bootRun {
    systemProperties = System.properties //pass through system properties
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}
docker {
    name "${project.group}/${bootJar.baseName}"
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}